{
  parserClass="com.github.xepozz.crontab.language.parser.CrontabParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Crontab"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.xepozz.crontab.language.psi"
  psiImplPackage="com.github.xepozz.crontab.language.psi.impl"

  elementTypeHolderClass="com.github.xepozz.crontab.language.psi.CrontabTypes"
  elementTypeClass="com.github.xepozz.crontab.language.psi.CrontabElementType"
  tokenTypeClass="com.github.xepozz.crontab.language.psi.CrontabTokenType"

  psiImplUtilClass="com.github.xepozz.crontab.language.psi.impl.CrontabImplUtil"
}

crontabFile ::= item_*

private item_ ::= cronExpression | variableDefinition | COMMENT | NEWLINE

cronExpression ::= SCHEDULE COMMAND
{
    pin=1
    implements=["com.intellij.psi.NavigatablePsiElement"]
}

variableDefinition ::= (VARIABLE_NAME EQUAL_SIGN VARIABLE_VALUE)
{
    pin=1
    implements=["com.intellij.psi.NavigatablePsiElement"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabVariableBaseImpl"
}
VARIABLE_NAME ::= IDENTIFIER
VARIABLE_VALUE ::= CONTENT

TIME_SHORTCUT ::= AT CONTENT
{
    implements=["com.intellij.psi.NavigatablePsiElement" "com.intellij.psi.PsiLiteralValue"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabTimeShortcutBaseImpl"
}

SCHEDULE ::= (TIME_POINTER TIME_POINTER TIME_POINTER TIME_POINTER TIME_POINTER) | TIME_SHORTCUT
{
    implements=["com.intellij.psi.NavigatablePsiElement" "com.intellij.psi.PsiLiteralValue"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabScheduleBaseImpl"
}

TIME_POINTER ::= TIME_LIST
TIME_LIST ::= TIME_LIST_ITEM (COMMA TIME_LIST_ITEM)*

TIME_LIST_ITEM ::= TIME_RANGE_STEP | TIME_RANGE | TIME_RANGE_DAY | TIME_RANGE_MONTH | TIME_EXACT_NUMBER | TIME_EXACT_DAY | TIME_EXACT_MONTH | TIME_ANY

TIME_RANGE_STEP ::= (TIME_RANGE | TIME_EXACT_NUMBER | TIME_ANY) SLASH NUMBER
{
    pin=2
}

TIME_RANGE ::= NUMBER HYPHEN NUMBER
{
    pin=2
    implements=["com.intellij.psi.NavigatablePsiElement"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabTimeRangeBaseImpl"
    methods=[getFirst getSecond getIntRange]
}
TIME_RANGE_DAY ::= DAY HYPHEN DAY
TIME_RANGE_MONTH ::= MONTH HYPHEN MONTH

TIME_ANY ::= STAR
TIME_EXACT_NUMBER ::= NUMBER
TIME_EXACT_DAY ::= DAY
TIME_EXACT_MONTH ::= MONTH

COMMAND ::= CONTENT | NEWLINE
{
    implements=["com.intellij.psi.NavigatablePsiElement" "com.intellij.psi.PsiLanguageInjectionHost"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabCommandBaseImpl"
}

COMMENT ::= SINGLE_COMMENT
