{
  parserClass="com.github.xepozz.crontab.language.parser.CrontabParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  extends(".*_pattern")=time_pointer

  psiClassPrefix="Crontab"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.xepozz.crontab.language.psi"
  psiImplPackage="com.github.xepozz.crontab.language.psi.impl"

  elementTypeHolderClass="com.github.xepozz.crontab.language.psi.CrontabTypes"
  elementTypeClass="com.github.xepozz.crontab.language.psi.CrontabElementType"
  tokenTypeClass="com.github.xepozz.crontab.language.psi.CrontabTokenType"

  psiImplUtilClass="com.github.xepozz.crontab.language.psi.impl.CrontabImplUtil"
}

crontab_file ::= item_*

private item_ ::= variable_definition | cron_expression | SINGLE_COMMENT | EOL

cron_expression ::= schedule command
{
    pin=1
    implements=["com.intellij.psi.NavigatablePsiElement"]
    recoverWhile=cron_expression_recover
}
private cron_expression_recover ::= !(EOL)

variable_definition ::= (variable_name EQUAL_SIGN variable_value)
{
    pin=1
    implements=["com.intellij.psi.NavigatablePsiElement"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabVariableBaseImpl"
}
variable_name ::= IDENTIFIER
variable_value ::= CONTENT

time_shortcut ::= AT CONTENT
{
    implements=["com.intellij.psi.NavigatablePsiElement" "com.intellij.psi.PsiLiteralValue"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabTimeShortcutBaseImpl"
}

schedule ::= (minute_pattern hour_pattern day_pattern month_pattern week_pattern) | time_shortcut
{
    implements=["com.intellij.psi.NavigatablePsiElement" "com.intellij.psi.PsiLiteralValue"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabScheduleBaseImpl"
}

minute_pattern ::= time_pointer
hour_pattern ::= time_pointer
day_pattern ::= time_pointer
month_pattern ::= time_pointer
week_pattern ::= time_pointer

time_pointer ::= time_list
time_list ::= time_list_item (COMMA time_list_item)*

time_list_item ::= time_range_step | time_range | time_range_day | time_range_month | time_exact_number | time_exact_day | time_exact_month | time_any

time_range_step ::= (time_range | time_exact_number | time_any) SLASH NUMBER
{
    pin=2
}

time_range ::= NUMBER HYPHEN NUMBER
{
    pin=2
    implements=["com.intellij.psi.NavigatablePsiElement"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabTimeRangeBaseImpl"
    methods=[getFirst getSecond getIntRange]
}
time_range_day ::= DAY HYPHEN DAY
time_range_month ::= MONTH HYPHEN MONTH

time_any ::= STAR
time_exact_number ::= NUMBER
time_exact_day ::= DAY
time_exact_month ::= MONTH

command ::= CONTENT | EOL
{
    implements=["com.intellij.psi.NavigatablePsiElement" "com.intellij.psi.PsiLanguageInjectionHost"]
    extends="com.github.xepozz.crontab.language.psi.impl.CrontabCommandBaseImpl"
}
